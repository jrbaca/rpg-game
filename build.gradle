plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.10'
    id 'application'
}

mainClassName = 'com.josephbaca.rpggame.GameWebInterface'

group 'com.josephbaca.rpggame'
version '1.0.0'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform() // necessary to activate Gradle's JUnit5 support
    outputs.upToDateWhen { false }

    minHeapSize = "512m"
    maxHeapSize = "2048m"
    jvmArgs = ["-Xss128m"] // bigger stack as well
}


task stage {
    dependsOn 'clean', 'installDist'
}
installDist.mustRunAfter clean


dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8' // Kotlin
    
    // Logging
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25' // Logger API
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3' // LogBack

    // Web stuff
    compile('com.sparkjava:spark-core:2.7.2') { // Web server
        exclude module: 'slf4j-simple' // Using logback
    }
    compile 'com.j2html:j2html:1.0.0' // HTML builder
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.7' // JSON

    // Testing libraries: these are only visible from our "test" code, not the "main" code
    testCompile 'org.mockito:mockito-core:2.19.0' // mock testing library
    testCompile 'org.quicktheories:quicktheories:0.25' // pattern-based testing library

    // JUnit5 support
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
}